import ch.szclsb.orbis.plugins.MathPlugin

plugins {
    id 'java'
}

group 'ch.szclsb'
version '1.0-SNAPSHOT'

apply plugin: MathPlugin

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':processor')

    annotationProcessor project(':processor')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

math {
    matrices {
        register('FMatrix4x4') {
            type = 'float'
            rows = 4
            columns = 4
        }
    }
}

compileJava {
    options.compilerArgs += ['--add-modules', 'jdk.incubator.vector']
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileTestJava {
    options.compilerArgs += ['--add-modules', 'jdk.incubator.vector']
}

test {
    jvmArgs('--add-modules', 'jdk.incubator.vector')
    useJUnitPlatform() {
        includeTags "UnitTest"
    }
}

tasks.register("performanceTest", Test) {
    jvmArgs('--add-modules', 'jdk.incubator.vector')
    useJUnitPlatform() {
        includeTags "PerformanceTest"
    }
    mustRunAfter test
}


